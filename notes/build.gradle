apply plugin: 'com.android.application'

def keystoreFilepath = ''
def keystoreFile
def keystorePassword = ''
def keystoreAlias = ''
def keystoreAliasPassword = ''

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
keystoreFilepath = properties.getProperty("keystore.path")
if (keystoreFilepath) {
    keystoreFile = file(keystoreFilepath)
    keystorePassword = properties.getProperty("keystore.password")
    keystoreAlias = properties.getProperty("keystore.alias")
    keystoreAliasPassword = properties.getProperty("keystore.alias_password")
}

def releaseTime() {
    return new Date().format("HH:mm:ss")
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"
    signingConfigs {
        release {
            storeFile keystoreFile
            storePassword keystorePassword
            keyAlias keystoreAlias
            keyPassword keystoreAliasPassword
        }
    }
    defaultConfig {
        applicationId "com.wuzhanglao.niubi"
        minSdkVersion 14
        targetSdkVersion 24
        versionCode 1
        versionName ""
        ndk {
            //选择要添加的对应cpu类型的.so库。
            abiFilters 'armeabi', 'armeabi-v7a', 'armeabi-v8a', 'x86', 'x86_64', 'mips', 'mips64'
        }

        manifestPlaceholders = [
                JPUSH_PKGNAME: applicationId,
                JPUSH_APPKEY : "bdff410f9c7a31c23daad7aa", //JPush上注册的包名对应的appkey.
                JPUSH_CHANNEL: "developer-default", //暂时填写默认值即可.
        ]
    }
    buildTypes {
        debug {
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"
            versionNameSuffix "-debug"
        }

        release {
            //不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"
            //混淆
            minifyEnabled true
            //Zipalign优化
            zipAlignEnabled true
            //移除无用的resource文件
            shrinkResources true
            //加载默认混淆配置文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (keystoreFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
    }
    dexOptions {
        preDexLibraries true
        dexInProcess = true
        javaMaxHeapSize "4g"
    }
    productFlavors {
        dev {
            // 注意，这里是添加，在 string.xml 不能有这个字段，会重名！！！
            resValue "string", "app_name", "Notes测试版"
        }
        real {
            // 动态添加 string.xml 字段；
            // 注意，这里是添加，在 string.xml 不能有这个字段，会重名！！！
            resValue "string", "app_name", "Notes"
        }
    }
    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                //Notes+版本名字+时间
                def fileName = "notes_${releaseTime()}.apk"
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    //    compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support:support-v4:24.0.0'
    compile 'com.android.support:recyclerview-v7:24.0.0'
    compile 'com.android.support:cardview-v7:24.0.0'
    compile 'com.android.support:appcompat-v7:24.0.0'
    //    compile 'com.google.code.gson:gson:2.2.4'
    //网络图片加载
    compile 'com.github.bumptech.glide:glide:3.7.0'
    //rxjava相关依赖
    compile 'io.reactivex:rxjava:1.2.1'
    compile 'io.reactivex:rxandroid:1.2.1'
    //retrofit相关依赖
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    //okhttp相关依赖
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'
    //友盟统计
    compile 'com.umeng.analytics:analytics:latest.integration'
    //JPush
    compile 'cn.jiguang:jpush:2.1.8'
    // 此处以SDK 2.1.8版本为例
    //bmob-sdk：Bmob的android sdk包，包含了Bmob的数据存储、文件等服务，以下是最新的bmob-sdk:
    //    compile('cn.bmob.android:bmob-sdk:3.5.0') {
    //        exclude group: 'com.squareup.okhttp3'
    //        exclude group: 'com.google.gson',module:'annotations'
    //    }
    //    compile 'cn.bmob.android:http-legacy:1.0'
}
